apply plugin: 'com.android.application'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply from: "$project.rootDir/config/dependencies.gradle"
apply from: "$project.rootDir/config/app-version.gradle"
apply plugin: 'io.fabric'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    flavorDimensions "default"
    defaultConfig {
        applicationId appApplicationId
        minSdkVersion androidMinSdkVersion
        targetSdkVersion androidTargetSdkVersion
        versionCode appVersionCode
        versionName appVersionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            it.buildConfigField("String", "PANTAU_BASE_URL", PANTAU_BASE_URL_PROD)
            it.buildConfigField("String", "PANTAU_OAUTH_URL", PANTAU_OAUTH_URL_PROD)
            it.buildConfigField("String", "WORD_STADIUM_BASE_URL", WORD_STADIUM_BASE_URL_PROD)
        }
        debug {
            applicationIdSuffix ".staging"
            minifyEnabled false
            debuggable true
            it.buildConfigField("String", "PANTAU_BASE_URL", PANTAU_BASE_URL_STAG)
            it.buildConfigField("String", "PANTAU_OAUTH_URL", PANTAU_OAUTH_URL_STAG)
            it.buildConfigField("String", "WORD_STADIUM_BASE_URL", WORD_STADIUM_BASE_URL_STAG)
        }

        buildTypes.each {
            it.buildConfigField("String", "CLIENT_ID", CLIENT_ID)
            it.buildConfigField("String", "CLIENT_SECRET", CLIENT_SECRET)
        }
    }

    productFlavors {
        production {
        }
        staging {
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }

}

repositories {
    jcenter()
}

configurations {
    ktlint
}

dependencies {
    implementation project(':instant')
    implementation project(':base')

    implementation supportLibs
    implementation networkLibs
    implementation firebaseLibs
    implementation rxJavaLibs
    implementation multidex
    implementation easyPermission
    implementation timber
    implementation glide
    implementation roundedImageView
    implementation symbolic
    implementation flexbox
    implementation dagger
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:support-v4:28.0.0'
    kapt daggerCompiler

    testImplementation unitTestLibs

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    ktlint "com.github.shyiko:ktlint:0.29.0"
}


task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "src/**/*.kt"
    // args "--reporter=checkstyle, output=${buildDir}/ktlint.xml
}
apply plugin: 'com.google.gms.google-services'
